<!doctype html>
<html>
  <body style="margin: 0px; overflow: hidden">
    <canvas id="canvas" style="position: absolute; width: 100%; height: 100%" />
  </body>

  <script type="x-shader/x-vertex" id="vertex-shader">
    #version 100

    attribute vec2 vertexPosition;
    varying vec2 vVertexPosition;

    void main() {
        gl_Position = vec4(vertexPosition, 0.0, 1.0);
        vVertexPosition = vertexPosition;
    }
  </script>
  <script type="x-shader/x-fragment" id="fragment-shader">
    #version 100
    #ifdef GL_ES
    precision mediump float;
    #endif

    #define PI 3.1415926535898

    varying vec2 vVertexPosition;
    uniform float time;
    uniform float aspect_ratio;
    uniform vec2 mouse;

    const int max_iterations = 48;
    const float step_scale = 0.9;
    const float eps = 0.005;

    // ---------------------------------------------------------------------------------------------
    /** Structure definitions.
    */

    struct Ray {
        vec3 o; ///< origin
        vec3 d; ///< direction
    };

    // ---------------------------------------------------------------------------------------------
    /** Object distance functions and noise functions.
    dObject - signed distance function of object.
    udObject - unsigned distance function of object.
    */
    float dSphere(const vec3 p, const float radius) {
        return length(p) - radius;
    }

    float dRoundBox(const vec3 p, const vec3 box_sides_length, const float roundness) {
      return length(max(abs(p)-box_sides_length,0.0))-roundness;
    }

    float udBox(const vec3 p, const vec3 b){
        return length(max(abs(p)-b,0.0));
    }

    float dSinusoidalPlasma(const vec3 p){
        return sin(p.x+time*2.)*cos(p.y+time*2.1)*sin(p.z+time*2.3) + 0.25*sin(p.x*2.)*cos(p.y*2.)*sin(p.z*2.);
    }

    // ---------------------------------------------------------------------------------------------
    /** Transformation functions.
    */
    vec3 opRepete(const vec3 p, const vec3 c) {
        // returns a new point given point p and a tiling-vector c,
        // the bigger, the compontents of c, the bigger the tile along
        // the according dinmension.
        return mod(p, c) - 0.5 * c;
    }

    // TODO not only repete, but also rndmly offset the y's acc. to different repetitions,
    // e.g. (pseudo)randomly shift objects in global space according to given tiling.

    vec3 opTwist(const vec3 p, const float intensity) {
        float c = cos(intensity*p.y);
        float s = sin(intensity*p.y);
        mat2  m = mat2(c,-s,s,c);
        return vec3(m*p.xz,p.y);
    }

    vec3 opRotateX(const vec3 p, const float radians) {
        float sin_rot = sin(radians);
        float cos_rot = cos(radians);
        mat3 m = mat3( 1.0,        0.0,        0.0,
                        0.0,    cos_rot,    sin_rot,
                        0.0,    -sin_rot,   cos_rot);
        return m*p;
    }

    vec3 opRotateY(const vec3 p, const float radians) {
        float sin_rot = sin(radians);
        float cos_rot = cos(radians);
        mat3 m = mat3( cos_rot,    0.0,    -sin_rot,
                        0.0,        1.0,    0.0,
                        sin_rot,    0.0,    cos_rot);
        return m*p;
    }

    vec3 opRotateZ(const vec3 p, const float radians) {
        float sin_rot = cos(radians);
        float cos_rot = sin(radians);
        mat3 m = mat3( cos_rot,    sin_rot,    0.0,
                        -sin_rot,    cos_rot,    0.0,
                        0.0,        0.0,        1.0);
        return m*p;
    }

    // ---------------------------------------------------------------------------------------------
    /** Scene function.
    reminder: simple combinations of volumetric objects:
    - union: min(a,b)
    - cut: max(a,b)
    - minus: max(a,-b)
    */
    float scene(const vec3 p) {
        vec3 q = p;

        q = opRepete(p-vec3(1), vec3(2));

        return min(
            dSphere(
                p - vec3(
                    0.6*sin(time*3.0)*sin(time*2.0),
                    0.6*sin(time*3.0)*cos(time*2.0),
                    0.6*cos(time*3.0)),
                0.05),
            dSphere(
                q,
                0.5) + 0.03 * dSinusoidalPlasma(vec3(6)*p));
    }

    vec3 getNormal(const vec3 p) {
        /** Retrieves an interpolated, estimated surface normal at a given point.
        */
        // 6-tap normalization. Probably the most accurate, but a bit of a cycle waster.
        /*
        return normalize(vec3(
            scene(vec3(p.x+eps,p.y,p.z))-scene(vec3(p.x-eps,p.y,p.z)),
            scene(vec3(p.x,p.y+eps,p.z))-scene(vec3(p.x,p.y-eps,p.z)),
            scene(vec3(p.x,p.y,p.z+eps))-scene(vec3(p.x,p.y,p.z-eps))
       ));
        /**/
        // If speed is an issue, here's a slightly-less-accurate, 4-tap version. If fact,
        // visually speaking, it's virtually the same, so on a lot of occasions,
        // this is the one I'll use. However, if speed is really an issue, you could
        // take away the "normalization" step, then divide by "eps," but I'll usually avoid
        // doing that.
        /*
        float ref = scene(p);
        return normalize(vec3(
            scene(vec3(p.x+eps,p.y,p.z))-ref,
            scene(vec3(p.x,p.y+eps,p.z))-ref,
            scene(vec3(p.x,p.y,p.z+eps))-ref
       ));
        /**/
        // The tetrahedral version, which does involve fewer calculations, but doesn't seem
        // as accurate on some surfaces. I could be wrong, but that's the impression I get.
        vec2 e = vec2(-0.5*eps,0.5*eps);
        return normalize(e.yxx*scene(p+e.yxx)+e.xxy*scene(p+e.xxy)+e.xyx*scene(p+e.xyx)+e.yyy*scene(p+e.yyy));
        /**/
    }

    float rayMarching(in Ray r, const float start, const float end) {
        float dist_to_scene;
        float ray_length = start;
        for (int i = 0; i < max_iterations; ++i) {
            if( ray_length >= end) {
                return end;
            }

            dist_to_scene = scene(r.o + r.d * ray_length);
            if(dist_to_scene < eps) {
                break;
            } else {
                ray_length += dist_to_scene * step_scale;
            }
        }

        return ray_length;
    }

    Ray setupCam(const vec2 uv, const vec3 origin, const vec3 look_at, const vec2 FOV) {
        vec3 forward = normalize(look_at-origin);
        vec3 right   = normalize(vec3(forward.z, 0.0, -forward.x));
        vec3 up      = normalize(cross(forward, right));

        return Ray(
            origin,
            normalize(forward + FOV.x*uv.x*right + FOV.y*uv.y*up));
    }

    void main(void) {
        vec2 uv = vec2(vVertexPosition.x * aspect_ratio, vVertexPosition.y);

        Ray cam = setupCam(
            uv,
            vec3((mouse.x-0.5)*3.0, 0.0, -4.0+(mouse.y)*3.0),
            vec3(0.,0.,0.),
            vec2(1.0));

        const vec4 bg_color = vec4(0.15,0.0,0.0,1.0); // the back color

        const float clip_near = 1.0;
        const float clip_far = 10.0;
        float dist_to_scene = rayMarching(cam, clip_near, clip_far);
        if (dist_to_scene >= clip_far) {
            gl_FragColor = bg_color;
            return;
        }

        vec3 surface_pos = cam.o + cam.d*dist_to_scene;
        vec3 surface_normal = getNormal(surface_pos);

        vec3 light_color = vec3(1.,0.97,0.92);
        vec3 light_pos = vec3(1.5*sin(time*2.0), 0.75+0.25*cos(time*2.0), -1.0);
        vec3 light_dir = light_pos-dist_to_scene;
        float light_to_point_dist = length(light_dir);
        light_dir /= light_to_point_dist;
        float light_attenuation = min(1.0, 1.0 / (0.025*light_to_point_dist*light_to_point_dist));
        vec3 light_reflection_dir = reflect(-light_dir, surface_normal);

        vec3 color = vec3(0.2,0.0,0.0);

        // The spherical object's color. My favorite color is black, but I don't think that will work, so I've gone with something greenish.
        vec3 obj_color = vec3(0.5, 1.0, 0.1);

        vec3 voxPos = mod(surface_pos*0.5, 1.0);
    if ( (voxPos.x<0.5)&&(voxPos.y>=0.5)&&(voxPos.z<0.5) )
        obj_color = vec3(1.0,voxPos.z*0.5,0.0);
    else if ( (voxPos.x>=0.5)&&(voxPos.y<0.5)&&(voxPos.z>=0.5) )
        obj_color = vec3(0.0,0.5+voxPos.z*0.5,1.0);


    float fakeShadowMovement =  dSinusoidalPlasma(surface_pos*8.);
    obj_color = clamp(obj_color*(0.75-0.25*fakeShadowMovement), 0.0, 1.0);



        const float obj_ambient = 0.1;
        const float obj_specular_power = 360.0;
        float obj_diffuse  = max(0.0, dot(surface_normal, light_dir));
        float obj_specular = max(0.0, dot(light_reflection_dir, normalize(cam.o-surface_pos)));
        obj_specular = pow(obj_specular, obj_specular_power);

        color += (obj_color*(obj_diffuse*0.8 + obj_ambient) + obj_specular*0.5) * light_color * light_attenuation;

        gl_FragColor = vec4(clamp(color, 0.0, 1.0), 1.0);
    }
  </script>

  <script type="text/javascript" id="webgl-setup">
    "use strict";

    var fps = 0;
    var fpsTime = 0.0;
    var lastFrameTime = Date.now();

    var canvas = document.querySelector("canvas");
    canvas.width = canvas.clientWidth;
    canvas.height = canvas.clientHeight;
    var gl = canvas.getContext("webgl2");

    const timerStart = Date.now();

    var uniforms = {
      time: function (v) {
        gl.uniform1f(gl.getUniformLocation(shaderProgram, "time"), v);
      },
      mouse: function (v) {
        gl.uniform2fv(gl.getUniformLocation(shaderProgram, "mouse"), v);
      },
      aspectRatio: function (v) {
        gl.uniform1f(gl.getUniformLocation(shaderProgram, "aspect_ratio"), v);
      },
    };

    gl.viewport(0, 0, gl.drawingBufferWidth, gl.drawingBufferHeight);
    var shaderProgram = compileShaderProgram(
      gl,
      document.querySelector("#vertex-shader").innerHTML,
      document.querySelector("#fragment-shader").innerHTML,
    );
    gl.useProgram(shaderProgram);
    createFullscreenQuad(gl, shaderProgram);

    window.addEventListener("mousemove", onMouseMove);
    window.addEventListener("resize", onWindowResize);
    window.addEventListener("beforeunload", cleanup, true);

    onWindowResize();
    render();

    function compileShaderProgram(gl, vertexShaderSource, fragmentShaderSource) {
      let vertexShader = gl.createShader(gl.VERTEX_SHADER);
      gl.shaderSource(vertexShader, vertexShaderSource);
      gl.compileShader(vertexShader);
      if (!gl.getShaderParameter(vertexShader, gl.COMPILE_STATUS)) {
        alert(
          "Vertex shader did not compile successfully.\n" +
            "Info log: " +
            gl.getShaderInfoLog(vertexShader),
        );
      }

      let fragmentShader = gl.createShader(gl.FRAGMENT_SHADER);
      gl.shaderSource(fragmentShader, fragmentShaderSource);
      gl.compileShader(fragmentShader);
      if (!gl.getShaderParameter(fragmentShader, gl.COMPILE_STATUS)) {
        alert(
          "Fragment shader did not compile successfully.\n" +
            "Info log: " +
            gl.getShaderInfoLog(fragmentShader),
        );
      }

      let shaderProgram = gl.createProgram();
      gl.attachShader(shaderProgram, vertexShader);
      gl.attachShader(shaderProgram, fragmentShader);
      gl.linkProgram(shaderProgram);
      gl.detachShader(shaderProgram, vertexShader);
      gl.deleteShader(vertexShader);
      gl.detachShader(shaderProgram, fragmentShader);
      gl.deleteShader(fragmentShader);
      if (!gl.getProgramParameter(shaderProgram, gl.LINK_STATUS)) {
        alert(
          "Shader shaderProgram did not link successfully.\n" +
            "Info log: " +
            gl.getProgramInfoLog(shaderProgram),
        );
      }
      return shaderProgram;
    }

    function onWindowResize(event) {
      uniforms.aspectRatio([canvas.clientWidth / canvas.clientHeight]);
    }

    function onMouseMove(event) {
      uniforms.mouse([event.pageX / canvas.clientWidth, event.pageY / canvas.clientHeight]);
    }

    function cleanup() {
      if (gl) {
        gl.useProgram(null);
      }
      if (shaderProgram) {
        gl.deleteProgram(shaderProgram);
      }
    }

    function createFullscreenQuad(gl, shaderProgram) {
      const vertexArray = new Float32Array([-1.0, -1.0, -1.0, 1.0, 1.0, -1.0, 1.0, 1.0]);

      gl.bindBuffer(gl.ARRAY_BUFFER, gl.createBuffer());
      gl.bufferData(gl.ARRAY_BUFFER, vertexArray, gl.STATIC_DRAW);

      const vertexPositionLoc = gl.getAttribLocation(shaderProgram, "vertexPosition");
      gl.enableVertexAttribArray(vertexPositionLoc); // enables using attrib vals from buffer array
      gl.vertexAttribPointer(
        vertexPositionLoc,
        2 /*no of entries per vertex belonging to this attrib in the buffer*/,
        gl.FLOAT /* type of entries*/,
        false /*is normalized*/,
        8 /*total no bytes of one vertex in buffer, e.g the stride*/,
        0 /*bytes offset per vertex for this attribute*/,
      );
    }

    function render() {
      window.requestAnimationFrame(render);

      uniforms.time((Date.now() - timerStart) * 0.001);

      // gl.clear(gl.COLOR_BUFFER_BIT);
      gl.drawArrays(gl.TRIANGLE_STRIP, 0 /*vertex-offset*/, 4 /*vertex-count*/);

      fps++;
      fpsTime += Date.now() - lastFrameTime;
      lastFrameTime = Date.now();
      if (fpsTime >= 1000.0) {
        fpsTime -= 1000.0;
        document.title = "Raymarching Template @" + fps + " fps";
        fps = 0;
      }
    }
  </script>
</html>
